/*******************************************************************************
 * Copyright (c) 2017 itemis AG and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Zoey Gerrit Prigge  - initial API and implementation (bug #454629)
 *    
 *******************************************************************************/
grammar org.eclipse.gef.dot.internal.language.DotRecordLabel hidden(WS)

generate recordlabel "http://www.eclipse.org/gef/dot/internal/language/DotRecordLabel"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RecordLabel:
	fields+=AbstractField ('|' fields+=AbstractField)*;

AbstractField:
	BaseField | RotationWrapper;

RotationWrapper:
	'{' label=RecordLabel '}';

BaseField:
	{BaseField} (port=Port)? (name=UNQUOTEDSTRING)?;

Port:
	{Port} '<' (name=UNQUOTEDSTRING)? '>';

MayHaveName:
	Port | BaseField;

	/*
	 * This string is either a single character or a string not beginning with whitespace
	 * Note ideally, should not end in whitespace either, but that seems impossible.
 	 */
terminal UNQUOTEDSTRING:
	('\\' . | !('|' | '{' | '}' | '<' | '>' | '\\' | ' ' | '\t' | '\r' | '\n')) ('\\' .
	| !('|' | '{' | '}' | '<' | '>' | '\\'))*;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;